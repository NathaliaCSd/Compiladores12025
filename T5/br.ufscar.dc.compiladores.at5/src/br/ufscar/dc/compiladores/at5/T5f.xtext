grammar br.ufscar.dc.compiladores.at5.T5f with org.eclipse.xtext.common.Terminals

generate t5f "http://www.ufscar.br/dc/compiladores/at5/T5f"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Programa:
    'DECLARACOES' declaracoes+=Declaracao+
    'ALGORITMO' corpo=Corpo 'FIM_ALGORITMO'
;

Declaracao:
      DeclaracaoLocal
    | DeclaracaoGlobal
;

DeclaracaoLocal:
      'declare' variaveis+=Variavel (',' variaveis+=Variavel)*
    | 'constante' name=ID ':' tipoBasico=TipoBasico '=' valor=ValorConstante
    | 'tipo' name=ID ':' tipoDef=Tipo
;

DeclaracaoGlobal:
      'procedimento' name=ID '(' (parametros=Parametros)? ')' corpo=Corpo 'fim_procedimento'
    | 'funcao' name=ID '(' (parametros=Parametros)? ')' ':' tipoRetorno=TipoEstendido corpo=Corpo 'fim_funcao'
;

Variavel:
    name=Identificador ':' tipoVar=Tipo
;

Identificador:
    name=ID ('.' parts+=ID)* dimensoes+=Dimensao*
;
Dimensao:
    '[' exp=ExpressaoAritmetica ']'
;

Tipo:
      registro=Registro
    | tipoExt=TipoEstendido
;

Registro:
    {Registro} 'registro' (campos+=Variavel)* 'fim_registro'
;

TipoEstendido:
    '^'? (basic=TipoBasico | ref=[Declaracao])
;

TipoBasico:
    'literal' | 'inteiro' | 'real' | 'logico'
;

ValorConstante:
    STRING | INT | REAL | 'verdadeiro' | 'falso'
;

Parametros:
    parametro+=Parametro (',' parametro+=Parametro)*
;

Parametro:
    ('var')? ids+=Identificador (',' ids+=Identificador)* ':' tipoParam=TipoEstendido
;

Corpo:
    {Corpo} (locais+=DeclaracaoLocal)* comandos+=Comando*
;

Comando:
      ComandoLeia
    | ComandoEscreva
    | ComandoSe
    | ComandoCaso
    | ComandoPara
    | ComandoEnquanto
    | ComandoFaca
    | ComandoAtribuicao
    | ComandoChamada
    | ComandoRetorne
;

ComandoLeia:
  'leia' '(' ('^')? alvo+=[Variavel|ID] (',' ('^')? alvo+=[Variavel|ID])* ')'
;

ComandoEscreva:
    'escreva' '(' exp+=Expressao (',' exp+=Expressao)* ')'
;

ComandoSe:
    'se' cond=Expressao 'entao' comandos+=Comando* ('senao' comandosElse+=Comando*)? 'fim_se'
;

ComandoCaso:
    'caso' exp=ExpressaoAritmetica 'seja' selecao+=Selecao* ('senao' comandosElse+=Comando*)? 'fim_caso'
;

ComandoPara:
    'para' var=ID '<-' inicio=ExpressaoAritmetica 'ate' fim=ExpressaoAritmetica 'faca' comandos+=Comando* 'fim_para'
;

ComandoEnquanto:
    'enquanto' cond=Expressao 'faca' comandos+=Comando* 'fim_enquanto'
;

ComandoFaca:
      'faca' comandos+=Comando* 'ate' cond=Expressao
;

ComandoAtribuicao:
     'atribua' target=[Variavel|ID] '<-' valor=Expressao
   ;

ComandoChamada:
    'chame' name=ID '(' (args+=Expressao (',' args+=Expressao)*)? ')'
;

ComandoRetorne:
    'retorne' exp=Expressao
;

Selecao:
    itens+=ItemSelecao+ ':' comandos+=Comando*
;

ItemSelecao:
    faixas+=NumeroIntervalo (',' faixas+=NumeroIntervalo)*
;

NumeroIntervalo:
    sinal?=OpUnario start=INT ('..' end=INT)?
;

OpUnario:
    '-'
;

ExpressaoAritmetica:
    termos+=TermoAritmetico (outros+=OutroTermoAritmetico)*
;

OutroTermoAritmetico:
    op=OpArit1 termo=TermoAritmetico
;

TermoAritmetico:
    fatores+=FatorAritmetico (outros+=OutroFatorAritmetico)*
;

OutroFatorAritmetico:
    op=OpArit2 fator=FatorAritmetico
;

FatorAritmetico:
    ('^')? ref=[Variavel|ID]
  | numero=INT
  | real=REAL
  | '(' exp=ExpressaoAritmetica ')'
  | str=STRING 
;

Expressao:
    logicos+=TermoLogico (op+=OpBool logicos+=TermoLogico)*
;

TermoLogico:
    nao?='nao'? rel=ExpressaoRelacional
;

ExpressaoRelacional:
    left=ExpressaoAritmetica (op=OpRel right=ExpressaoAritmetica)?
;

OpArit1:
    '+' | '-'
;

OpArit2:
    '*' | '/'
;

OpRel:
    '>' | '>=' | '<' | '<=' | '<>' | '='
;

OpBool:
    'e' | 'ou'
;

terminal REAL: INT '.' INT;

// Definição explícita dos terminais para resolver o conflito de palavras-chave
terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal INT returns ecore::EInt: ('0'..'9')+;
terminal STRING:
    '"' ( '\\' . /* escape sequence */ | !('\\'|'"') )* '"' |
    "'" ( '\\' . /* escape sequence */ | !('\\'|"'") )* "'";
terminal ML_COMMENT: '/*' -> '*/';
terminal SL_COMMENT: '//' !('\n'|'\r')* ('\r'? '\n')?;
terminal WS: (' '|'\t'|'\r'|'\n')+;